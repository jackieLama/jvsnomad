plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
    id 'me.modmuss50.mod-publish-plugin'
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    validateAccessTransformers = true

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers = ["src/main/resources/META-INF/accesstransformer.cfg"]
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    maven {
        url = "https://api.modrinth.com/maven"
    }
}

dependencies {
    compileOnly project(":common")
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")
}

publishMods {
    file = jar.archiveFile
    modLoaders.add("forge")

    curseforge {
        //forge only dependencies found on curseforge go here. same format as in the root build.gradle
    }

    modrinth {
        //forge only dependencies found on modrinth go here. same format as in the root build.gradle
    }
}